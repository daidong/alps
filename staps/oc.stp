%{
#include <linux/file.h>
#include <net/sock.h>
#include <linux/un.h>
#include <linux/tcp.h>
#ifdef CONFIG_USER_NS
#include <linux/user_namespace.h>
#endif
#ifdef STAPCONF_LINUX_UIDGID_H
#include <linux/uidgid.h>
#endif
%}

function task_file_handle_d_path:string (task:long, fd:long) %{ /* pure */
	struct task_struct *p = (struct task_struct *)((long)THIS->task);
	struct files_struct *files;
	char *page = NULL;
	struct file *filp;
	struct dentry *dentry;
	struct vfsmount *vfsmnt;
	char *path = NULL;

	rcu_read_lock();
	if ((files = kread(&p->files)) &&
	    // We need GFP_ATOMIC since we're inside a lock so we
	    // can't sleep.
	    (page = (char *)__get_free_page(GFP_ATOMIC)) &&
	    (filp = fcheck_files(files, THIS->fd))) {

#if LINUX_VERSION_CODE >= KERNEL_VERSION(2,6,26)
		/* git commit 9d1bc601 */
		path = d_path(&filp->f_path, page, PAGE_SIZE);
#else
		dentry = kread(&filp->f_dentry);
		vfsmnt = kread(&filp->f_vfsmnt);

		if (dentry && vfsmnt) {
			path = d_path(dentry, vfsmnt, page, PAGE_SIZE);
		}
#endif
		if (path && !IS_ERR(path)) {
			snprintf(THIS->__retvalue, MAXSTRINGLEN, "%s", path);
		}
	}
	CATCH_DEREF_FAULT();

	if (page) free_page((unsigned long)page);

	rcu_read_unlock();
%}

function filtout_richfs(execname)
{
  if (execname == "stapio" || execname == "stap" ||execname == "alps")
  return 1;
}

probe kprocess.create
{
  if (filtout_richfs(execname())) next;

  # new_pid = new process Id; pid() = current process Id,
  printf("%c%d %d %d\n", 97, new_pid, gettimeofday_ms(), pid());
}

probe syscall.exit_group
{
  if (filtout_richfs(execname())) next;
  printf("%c%d %d\n", 98, pid(), gettimeofday_ms());
}

probe nd_syscall.execve
{
  if (filtout_richfs(execname())) next;

  env = sprintf("PBS_JOBID=%s;PBS_JOBNAME=%s;PBS_JOBCOOKIE=%s", env_var("PBS_JOBID"), env_var("PBS_JOBNAME"), env_var("PBS_JOBCOOKIE"));
  printf("%c%d %d %s %s %s %s %s\n", 99, pid(), gettimeofday_ms(), execname(), argstr, env, filename, args);
}

probe nd_syscall.execve.return
{
  if (filtout_richfs(execname())) next;
  env = sprintf("PBS_JOBID=%s;PBS_JOBNAME=%s;PBS_JOBCOOKIE=%s", env_var("PBS_JOBID"), env_var("PBS_JOBNAME"), env_var("PBS_JOBCOOKIE"));
  printf("%c%d %d %s %s\n", 100, pid(), gettimeofday_ms(), retstr, env);
}

#probe syscall.close
probe kernel.function("sys_close")
{
  if (filtout_richfs(execname())) next;

  try {
  	filename=task_file_handle_d_path(task_current(), $fd);
  	if (filename == "") filename = "UNDEFINED";
	  printf("%c%d %d %s %d\n", 103, pid(), gettimeofday_ms(), filename, $fd);
  } catch (msg) {
  	printf("%c%d %d %s %d\n", 103, pid(), gettimeofday_ms(), msg, $fd);
  }
}


#probe kernel.function("do_filp_open")
#probe kernel.function("do_filp_open").return
probe kernel.function("do_sys_open").return
{
  if (filtout_richfs(execname())) next;

  if ($return != -1)
  {
    filename=task_file_handle_d_path(task_current(), $return); #$return is the fd
    if (filename == "") filename = "UNDEFINED";
    printf("%c%d %d %s %d %d\n", 101, pid(), gettimeofday_ms(), filename, $return, $flags);
  }
}